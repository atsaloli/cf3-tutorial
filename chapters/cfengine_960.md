
<!---
Filename: 960-000-Part-Title-0000-Additional\_Examples.md
-->

# Appendix C - Additional Examples



<!---
Filename: 960-010-Security-0000-Chapter-Title.md
-->

## Security Examples


\begin{codelisting}
\codecaption{960-010-Security-0010-Processes\_not\_running\_a\_blacklist.cf}
```cfengine3, options: "linenos": true
!! SKIP !!

body common control

{
        bundlesequence  => { "processes_not_running"  };
}


########################################


bundle agent processes_not_running {


  vars:

      "bad_process"     slist   =>      {
                                          "cupsd",
                                          "ircd",
                                          "ping",
                                          "portmap",
      };




  processes:
      "$(bad_process)"  signals => { "term", "kill" };

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-010-Security-0020-SELinux\_Allow\_httpd\_to\_connect\_to\_and\_to\_write\_to\_stream\_sockets.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  methods:
      "any" usebundle => allow_httpd_to_talk_to_pgbouncer;

}


bundle agent allow_httpd_to_talk_to_pgbouncer {

  classes:

      "module_missing"

        not => returnszero("/usr/sbin/semodule -l | \
/bin/grep allowHttpdSocketConnectWrite \
>/dev/null", "useshell");
# should be rewritten to use execresult and regcmp() so we can
# change "useshell" to "noshell" to make it more lightweight

  methods:
    module_missing::
      "any" usebundle => compile_and_package_and_load_selinux_module;

}


bundle agent compile_and_package_and_load_selinux_module {

  files:

      "/root/SELINUX/."
        create => "true",
        comment => "/root/SELINUX is my scratch space for SELinux
                    policy files as we compile, package and load
                    SELinux policy modules.";

  files:

      "/root/SELINUX/allowHttpdSocketConnectWrite.te"
        create => "true",
        perms  => m("0600"),
        edit_line => Allow_Httpd_Socket_Connect_and_Write,
        comment => "httpd connects to Postgres database through
                    pgbouncer.  we want httpd to connect to pgbouncer
                    using a socket file instead of over TCP/IP,
                    because we've had instances where PHP pages
                    that connect to the database a lot in quick
                    succession use up all available network ports
                    and subsequent connection attempts fail.";


  commands:

      "/usr/bin/checkmodule  -M -m \
-o /root/SELINUX/allowHttpdSocketConnectWrite.mod \
/root/SELINUX/allowHttpdSocketConnectWrite.te && \
/usr/bin/semodule_package \
-o /root/SELINUX/allowHttpdSocketConnectWrite.pp \
-m /root/SELINUX/allowHttpdSocketConnectWrite.mod && \
/usr/sbin/semodule \
-i /root/SELINUX/allowHttpdSocketConnectWrite.pp",
        comment => "Compile module; create package; load module.",
        contain => in_shell;

}


bundle edit_line Allow_Httpd_Socket_Connect_and_Write {

      delete_lines: ".*";

  insert_lines:
      "
# This file was generated by CFEngine

module allowHttpdSocketConnectWrite 1.0;

require {
type httpd_t;
type tmp_t;
type initrc_t;
class sock_file write;
class unix_stream_socket connectto;
}

#============= httpd_t ==============
allow httpd_t initrc_t:unix_stream_socket connectto;
allow httpd_t tmp_t:sock_file write;

"
        insert_type => "preserve_block";
}
```
\end{codelisting}

<!---
Filename: 960-010-Security-0030-Change\_detection.md
-->

## Security

### Change Detection
In the "computer immunology" research and development phase, Mark added file change detection capability to CFEngine.


\begin{codelisting}
\codecaption{960-010-Security-0040-Detect\_changes\_in\_etc.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  files:

      "/etc"

        handle       => "etc_tripwire",
        comment      => "Report changes on files in /etc",
        changes      => detect_all_change,
        depth_search => recurse("inf");
}


body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-010-Security-0050-Detect\_changes\_in\_etc\_Uses\_classes.cf}
```cfengine3, options: "linenos": true
bundle agent main {
  files:

      "/etc"

        handle       => "safeguard_files_in_etc",
        comment      => "Keep screaming about changes in /etc",
        changes      => detect_all_change_noupdate,
        depth_search => recurse("inf"),
        classes      => kept_repaired_failed("promise_kept",
                                             "promise_repaired",
                                             "promise_not_kept)");

  reports:

    promise_kept::

      "Kept";

    promise_repaired::
      "Repaired";

    promise_not_kept::

      "not kept";

}




body classes kept_repaired_failed(kept, repaired, failed) {
        promise_kept     => { "$(kept)" };
        promise_repaired => { "$(repaired)" };
        repair_failed    => { "$(failed)" };
        repair_denied    => { "$(failed)" };
        repair_timeout   => { "$(failed)" };
}


body changes detect_all_change_noupdate {
      # This is fierce, and will cost disk cycles
        hash           => "best";
        report_changes => "all";
        update_hashes  => "no";
}

##

body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-010-Security-0060-Match\_suspicious\_process\_names.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  vars:

      "suspicious_process_names"
        handle => "process_blacklist",
        comment => "Setup a list of known bad process names",
        slist =>
      {
        "sniff",
        "eggdrop",
        "r00t",
        "^\./",
        "john",
        "crack"
      };
  processes:
      "$(suspicious_process_names)"
        handle => "kill_bad_procs",
        comment => "Kill bad processes on sight",
        signals => { "term", "kill" };
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-010-Security-0070-Check\_open\_ports.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  vars:
      "listening_ports_and_processes_ideal_scene"
        handle => "expected_tcp_profile",
        comment => "expected network profile (listenting ports)",
        string => "22 sshd 80 httpd 443 httpd 5308 cf-server";

      # end of our expected configuration

  vars:
    centos_5::
      "listening_ports_and_processes"
        handle => "actual_tcp_profile",
        comment => "Our actual network profile",
        string =>
      execresult("/usr/sbin/lsof -i -n -P | \
/bin/grep LISTEN | \
/bin/sed -e 's#*:##' | \
/bin/grep -v 127.0.0.1 | \
/bin/grep -v ::1 | \
/bin/awk '{print $8,$1}' | \
/bin/sort | \
/usr/bin/uniq | \
/bin/sort -n | \
/usr/bin/xargs echo", "useshell");  # this is our
      # actual configuration.
      # we tell CFEngine to use a shell with "useshell"
      # to do a pipeline.

    centos_6::
      "listening_ports_and_processes"
        handle => "actual_tcp_profile",
        comment => "Our actual network profile",
        string =>
      execresult("/usr/sbin/lsof -i -n -P | \
/bin/grep LISTEN | \
/bin/sed -e 's#*:##' | \
/bin/grep -v 127.0.0.1 | \
/bin/grep -v ::1 | \
/bin/awk '{print $9,$1}' | \
/bin/sort | \
/usr/bin/uniq | \
/bin/sort -n | \
/usr/bin/xargs echo", "useshell");

  classes:
      "reality_does_not_match_ideal_scene"
      # check whether expected configuration matches actual.
        handle => "check_profile",
        comment => "Compare desired and actual configuration",
        not => strcmp (
                        "$(listening_ports_and_processes)",
                        "$(listening_ports_and_processes_ideal_scene)"
        );

  reports:
    reality_does_not_match_ideal_scene::
      "
DANGER!!!
DANGER!!!  Expected open ports and processes:
DANGER!!!  $(listening_ports_and_processes_ideal_scene)
DANGER!!!
DANGER!!!  Actual open ports and processes:
DANGER!!!  $(listening_ports_and_processes)
DANGER!!!
";  # and yell loudly if it does not match.
      # Note:  A "commands" promise could be used in
      # addition to "reports" to send a text message
      # to a sysadmin cell phone, or to feed
      # CRITICAL status to a monitoring system.
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-010-Security-0080-Configure\_sshd\_stub.cf}
```cfengine3, options: "linenos": true
bundle agent main {
  vars:

      "sshd[Protocol]"        string => "2";
      "sshd[X11Forwarding]"   string => "yes";
      "sshd[UseDNS]"          string => "no";

  methods:
      "any"    usebundle => edit_sshd("$(this.bundle).sshd");
}


bundle agent edit_sshd(params) {

  vars:
      "index" slist => getindices("$(params)");

  reports:
# pretend we're editing sshd.conf to set the above
      "$(index) :  $($(params)[$(index)])";
  files:
    "/tmp/sshd.conf"
      create => "true",
      edit_line => insert_lines("$(index) $($(params)[$(index)])");
}

bundle edit_line insert_lines(line) {

insert_lines: 
  "$(line)";
}

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-010-Security-0090-Use\_edit\_sshd.cf}
```cfengine3, options: "linenos": true
!! SKIP !!


# an example of a methods bundle accessing an
# array from the parent bundle.
#
# Arrays are not a data type in CFEngine; they
# are a data structure.
#
# Arrays cannot be "passed" as variables to a
# bundle or body. They are only referred to in
# some functions that search for their members
# and extract things like indices and values.
#
# So what you CAN pass is the name of the array,
# complete with context.

body file control
{
        inputs => {
                    "$(sys.libdir)/stdlib.cf",
                    "1870_More_Examples__Security__edit_sshd.cf"
        };

}

#########################################################


bundle agent main
{
  vars:
      "sshdconfig" string => "/etc/ssh/sshd_config";

      # SSHD configuration to set
      "sshd[Protocol]"                                string => "2";
      "sshd[X11Forwarding]"                           string => "yes";
      "sshd[UseDNS]"                                  string => "no";

  methods:
      "any"    usebundle => edit_sshd("$(sshdconfig)", "example.sshd");
}
```
\end{codelisting}

<!---
Filename: 960-020-More\_Examples-0000-Chapter-Title.md
-->

## More Examples


\begin{codelisting}
\codecaption{960-020-More\_Examples-0120-WordPress\_Diego.cf}
```cfengine3, options: "linenos": true
#Install WordPress:
#       1. Install Infrastructure:
#               1.1. Install httpd and mod_php and PHP MySQL client.
#               1.2. Install MySQL server.
#                       1.2.1. Create WordPress User in MySQL.
#                       1.2.2. Create WordPress Database in MySQL.
#               1.3. Make sure httpd and MySQL servers are running.
#       2. Install the PHP application (WordPress)
#               2.1. Download tarball with the latest version of WordPress
#                    PHP application.
#               2.2. Extract it into the httpd document root where it can
#                    be run by the Web server.
#               2.3. Create WordPress config file wp-config.php from
#                    wp-config-sample.php that's shipped with WordPress.
#               2.4. Tweak wp-config.php to put in the data needed
#                    to establish database connection (db name,
#                    db username and password).


bundle agent main {

  methods:

      "Install WordPress"
        usebundle => wordpress_install;

}

bundle agent wordpress_install
{
  vars:
      # Put all WordPress config settings in 'conf' array
      "conf[DB_NAME]"      string => "wordpress";
      "conf[DB_USER]"      string => "wordpress";
      "conf[DB_PASSWORD]"  string => "lopsa10linux";
      "conf[htmlroot]"     string => "/var/www/html";
      "conf[tarfile]"      string => "/root/wordpress-latest.tar.gz";
      "conf[wp_dir]"       string => "$(conf[htmlroot])/wordpress";
      "conf[conf]"    string => "$(conf[wp_dir])/wp-config.php";
      "conf[wp_cfgsample]" string => "$(conf[wp_dir])/wp-config-sample.php";

  methods:

      "Infrastructure"

        handle => "wp_infrastructure",
        comment => "httpd, PHP, MySQL and everything in-between",
        usebundle => wp_infrastructure;

      "Application"

        handle => "wp_application",
        comment => "Install and Configure WordPress PHP app",
        usebundle => wp_application;

}

bundle agent wp_application {

  methods:
      "any" usebundle => wp_tarball_is_present("wordpress_install.conf");
      "any" usebundle => wp_tarball_is_unrolled("wordpress_install.conf");
      "any" usebundle => conf_exists("wordpress_install.conf");
      "any" usebundle => wp_is_properly_configured("wordpress_install.conf");
}

bundle agent wp_infrastructure {

  methods:
      "any" usebundle => wp_packages_installed("wordpress_install.conf");
      "any" usebundle => wp_services_up("wordpress_install.conf");
      "any" usebundle => wp_mysql_configuration("wordpress_install.conf");
      #"any" usebundle => wp_allow_http_inbound("wordpress_install.conf");
}

#############################################

bundle agent wp_packages_installed(params)
{
  vars:
    debian::
      "desired_package" slist => {
                                   "apache2",
                                   "php5",
                                   "php5-mysql",
                                   "mysql-server",
      };
    redhat::
      "desired_package" slist => {
                                   "httpd",
                                   "php",
                                   "php-mysql",
                                   "mysql-server",
      };
  packages:
      "$(desired_package)"
        handle => "install_packages",
        comment => "Install needed packages",
        package_policy => "add",
        package_architectures => { "x86_64" },
        package_method => generic,
        classes => if_repaired("packages_added");

  commands:
    packages_added.debian::
      "/usr/sbin/service httpd graceful"
        comment => "Restarting httpd so it can pick up any new modules.";

  commands:
    packages_added.redhat::
      "/sbin/service httpd graceful"
        comment => "Restarting httpd so it can pick up any new modules.";
}

#############################################

bundle agent wp_services_up(params)
{
  processes:
      "mysqld" restart_class => "start_mysqld";
    redhat::
      "httpd"  restart_class => "start_httpd";
    debian::
      "apache2"  restart_class => "start_httpd";

  commands:
    start_mysqld&debian::
      "/usr/sbin/service mysqld start";

    start_mysqld&redhat::
      "/sbin/service mysqld start";

    start_httpd&redhat::
      "/sbin/service httpd start";

    start_httpd&debian::
      "/usr/sbin/service httpd start";
}

#############################################

bundle agent wp_tarball_is_present(params)
{

  classes:
      "wordpress_tarball_is_present"
        handle => "check_for_WP_tarball",
        comment => "check if we already have the WP tarball",
        expression => fileexists("$($(params)[tarfile])");

  commands:
    !wordpress_tarball_is_present::
      "/usr/bin/wget -q --timeout=10 \
                     -O $($(params)[tarfile]) \
                     http://wordpress.org/latest.tar.gz"
        handle => "download_WP_tarball",
        classes => if_repaired("we_have_WP_tarball"),
        comment => "Downloading latest version of WordPress.",
        action => logme("promise download_WP_tarball");
}

#############################################

bundle agent wp_tarball_is_unrolled(params)
{

  classes:
      "wordpress_directory_is_present"
        expression => fileexists("$($(params)[htmlroot])/wordpress/");

  commands:
    we_have_WP_tarball&(!wordpress_directory_is_present)::
      "/bin/tar -C $($(params)[htmlroot]) -xzf $($(params)[tarfile])"
        handle => "extract_tarball",
        depends_on => { "download_WP_tarball" },
        comment => "Unroll wordpress tarball to HTML document root";
}

#############################################

bundle agent wp_mysql_configuration(params)
{

  commands:
      "/usr/bin/mysql -u root -e \"
      CREATE DATABASE IF NOT EXISTS $($(params)[DB_NAME]);
      GRANT ALL PRIVILEGES ON $($(params)[DB_NAME]).*
      TO '$($(params)[DB_USER])'@localhost
      IDENTIFIED BY '$($(params)[DB_PASSWORD])';
      FLUSH PRIVILEGES;\"
"
        handle => "setup_db",
        comment => "Create DB, DB user, and access credentials";

}

#############################################

bundle agent conf_exists(params)
{

  classes:
      "wordpress_config_file_exists"
        handle => "check_WP_config_file_there",
        comment => "Check if WP config file is present",
        expression => fileexists("$($(params)[conf])");

  files:
    !wordpress_config_file_exists::
      "$($(params)[conf])"
        handle => "copy_WP_config_file",
        comment => "Copy WP config file from config sample file",
        copy_from => local_cp("$($(params)[wp_cfgsample])"),
        perms => m("a+r");
}

#############################################

bundle agent wp_is_properly_configured(params)
{
  vars:
      "wpparams" slist => getindices("$(params)");

  files:
      "$($(params)[conf])"
        handle => "configure_wordpress",
        comment => "Make sure wp-config.php is properly configured",
        edit_line => replace_or_add(
          "define\('$(wpparams)', *'(?!$($(params)[$(wpparams)])).*",
          "define('$(wpparams)', '$($(params)[$(wpparams)])');");
}

#############################################

bundle agent wp_allow_http_inbound(params)
{

  commands:
    iptables_edited::
      "/sbin/service iptables stop"
        comment => "Stopping iptables to allow inbound HTTP connections";
}

body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


body action logme(x)
{
        log_string => "$(sys.date) $(x)";

        log_kept => "/var/log/cfengine_keptlog.log";
        log_repaired => "/var/log/cfengine_replog.log";
        log_failed => "/var/log/cfengine_faillog.log";

}

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-020-More\_Examples-0160-Setting\_the\_environment\_for\_a\_command.cf}
```cfengine3, options: "linenos": true
body agent control
{
        environment => { "A=123", "B=456", "PGK_PATH=/tmp"};
}

############################################

bundle agent main
{
  commands:

      "/usr/bin/env"
        handle => "env_cmd",
        comment => "Demonstrate setting up the environment for a command";
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-020-More\_Examples-0170-Delete\_repo\_comments\_from\_CentOS\_repo\_and\_exclude\_postgresql.cf}
```cfengine3, options: "linenos": true
# edit CentOS repo file in /etc/yum.repos.d to exclude
 # Postgres packages from downloads/updates (because I want
# to get them from the Postgres.org repo).
#
# Note: I have to strip out the CentOS repo comments otherwise
# due to the nature of the section-aware editing, the comments
# end up in the middle of the previous section.

bundle agent main {

  methods:
      "any"
        usebundle => exclude_postgresql_from_CentOS_repo;

}


bundle agent exclude_postgresql_from_CentOS_repo {

  files:
      "/etc/yum.repos.d/CentOS-Base.repo"
        edit_line => DeleteRepoComments,
        handle => "CentOS_Base_repo__strip_repo_comments",
        comment => "Remove CentOS remarks about the repos,
                    they mess up section editing because
                    they are placed outside the section
                    they comment about.";

  files:
      "/etc/yum.repos.d/CentOS-Base.repo"
        edit_line => AppendIfNoLine(
"exclude=postgresql*$(const.n)# Get Postgres packages from PGDG$(const.n)"),
        comment => "Exclude postgresql packages in CentOS [base]
                    and [update] repos; we'll get them from
                    Postgres Global Development Group.";

}

########################################################

bundle edit_line DeleteRepoComments {
  delete_lines:
      "#released updates.*";
      "#packages used/produced in the build but not released";
      "#additional packages that may be useful";
      "#additional packages that extend functionality of existing packages";
      "#contrib - packages by Centos Users";
}

########################################################

bundle edit_line AppendIfNoLine(parameter) {

  insert_lines:
      "$(parameter)"
        select_region => MyINISection("base");


  insert_lines:
      "$(parameter)"
        select_region => MyINISection("updates");

}

########################################################



body select_region MyINISection(x)

{
        select_start => "\[$(x)\]";
        select_end => "\[.*\]";

      # This assumes a file format like:
      #
      # [section 1]
      #
      # lines....
      #
      # [section 2]
      #
      # lines... etc

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-020-More\_Examples-0180-Install\_php\_pecl\_module.cf}
```cfengine3, options: "linenos": true
# Install pecl_http PHP module to provide HttpRequest class
# to our PHP Web app:
#   - run "pecl install pecl_http" and set SELinux type
#     on http.so to textrel_shlib_t
#   - edit /etc/php.ini to tell php to dynamically load
#     http.so


body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

################################################################


bundle agent php_pecl_http_extension_is_installed_and_integrated {

  files:

      "/etc/php.ini"

        edit_line => tell_php_to_load_http_extension;



  classes:

      "no_http_so"
        not => fileexists("/usr/lib64/php/modules/http.so");


  commands:

    no_http_so::

      "/usr/bin/yes ' ' | \
       /usr/bin/pecl install pecl_http && \
       /usr/bin/chcon -t textrel_shlib_t /usr/lib64/php/modules/http.so" 
        comment => "Force the install to be non-interactive 
                    (let PECL install pecl_http with the default
                    settings instead of prompting us). 
                    Then set SELinux label.";

        contain => in_shell;
}


###############################################################


bundle edit_line tell_php_to_load_http_extension {
  vars:
      "dynamically_load_http_module"
        string => "extension=http.so ; XYZ requires pecl_http's HttpRequest";
        # this is the text we want in /etc/php.ini

  insert_lines:
      "$(dynamically_load_http_module)"
        location => in_Dynamic_Extensions_section;
}


###############################################################


body location in_Dynamic_Extensions_section

{
        before_after => "after";
        first_last => "first";
        select_line_matching => "; Dynamic Extensions ;";
}

# reloading the httpd if php.ini was edited
# is left as an exercise for the reader
# hint: if_repaired


# TODO: instead of using select_line_matching, use begin
# and end select region to insert the extension=http.so
# line into /etc/php.ini at the end of instead of in the
# middle of the Dynamic Extensions block so it looks cleaner.
```
\end{codelisting}

<!---
Filename: 960-030-EC2-0000-Chapter-Title.md
-->

## Amazon EC2 Examples



<!---
Filename: 960-030-EC2-0190-System\_provisioning\_and\_integration\_introduction.md
-->

## Amazon AWS EC2

Note: The following was a demo given at CasITConf 2011. Tighter integration with AWS may now exist in CFEngine.

Purpose: proof of concept of multi-node deployment, configuration and integration on Amazon EC2 cloud using CFEngine.

We start on a Ubuntu VM with Amazon EC2 CLI tools installed, courtesy of Florian Drescher of CloudTrainings.com. We configure it with our EC2 credentials.

Then we install CFEngine 3.1.4. Then we run casit_demo.cf to instantiate two servers, "web" and "db", and to install CFEngine 3.1.4 onto them. We then use that CFEngine to install Apache httpd and mod_php and WordPress PHP application on "web" and MySQL server on "db"; and we integrate the two servers. End result: a working instance of WordPress deployed across two servers.

Video: http://www.verticalsysadmin.com/cfengine/casit/


\begin{codelisting}
\codecaption{960-030-EC2-0200-system\_provisioning\_and\_integration\_demo.cf}
```cfengine3, options: "linenos": true
#############################################
bundle common global_vars {


      vars: "desired_servers" slist => {
                                         "web",
                                         "db",
      };
}

#############################################


body common control
{

        bundlesequence => {
"global_vars",
"no_hosts_known_to_ssh",
servers_provisioned(@{global_vars.desired_servers}),
hosts_file_distributed_and_loaded(@{global_vars.desired_servers}),
wordpress_installer_distributed_and_run(@{global_vars.desired_servers}),
        };


        inputs =>          { "$(sys.libdir)/stdlib.cf" };

}

#############################################


bundle agent no_hosts_known_to_ssh
{
  files:
      "/home/user/.ssh/known_hosts"
        delete => tidy;

      # I don't want to see SSH complaints about keys having changed

}

#############################################


bundle agent servers_provisioned(desired_servers)
{

  classes:
      "$(desired_servers)_up"
        expression => fileexists(
          "/home/user/cfengine_ec2/servers/$(desired_servers)"
                                );

      "$(desired_servers)_down"
        not => fileexists(
          "/home/user/cfengine_ec2/servers/$(desired_servers)"
                         );


  reports:

      "$(desired_servers) has been provisioned"
        ifvarclass => canonify("$(desired_servers)_up");

  commands:
      "/home/user/cfengine_ec2/start_micro_instance.sh $(desired_servers) \
        > /home/user/cfengine_ec2/servers/$(desired_servers)"
        ifvarclass => canonify("$(desired_servers)_down"),
        contain => in_shell;
}


#############################################

bundle agent hosts_file_distributed_and_loaded(desired_servers)
{
  commands:
      "/usr/bin/scp -o StrictHostKeyChecking=no \
                    -i /home/user/ec2/mysshkey_key.pem \
                    /etc/hosts ec2-user@$(desired_servers):hosts && \
       /usr/bin/ssh -t \
                    -o StrictHostKeyChecking=no
                    -i /home/user/ec2/mysshkey_key.pem
                    ec2-user@$(desired_servers)
                    sudo /bin/cp hosts /etc/hosts"
        contain => in_shell;
}

#############################################

bundle agent wordpress_installer_distributed_and_run(desired_servers)
{
  commands:
      "/usr/bin/scp -o StrictHostKeyChecking=no \
                    -i /home/user/ec2/mysshkey_key.pem \
             /home/user/cfengine_ec2/example102_wordpress_installation.cf \
                    ec2-user@$(desired_servers): && \
       /usr/bin/ssh -t \
                    -o StrictHostKeyChecking=no \
                    -i /home/user/ec2/mysshkey_key.pem \
                    ec2-user@$(desired_servers) \
                    sudo /usr/local/sbin/cf-agent -I \
                           -f ./example102_wordpress_installation.cf"
        contain => in_shell;
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-030-EC2-0210-system\_provisioning\_and\_integration\_wordpress\_installation.cf}
```cfengine3, options: "linenos": true
# Install WordPress:
# 1. Install Infrastructure:
#   1.1. Install httpd and mod_php and PHP MySQL client.
#   1.2. Install MySQL server.
#           1.2.1. Secure MySQL
#           1.2.2. Create WordPress User in MySQL.
#           1.2.3. Create WordPress Database in MySQL.
#   1.3. Make sure httpd and MySQL servers are running.
# 2. Install the PHP application (WordPress)
#   2.1. Download tarball with the latest version of WordPress PHP
#         application.
#   2.2. Extract it into the httpd document root where it can be
#        run by the Web server.
#   2.3. Create WordPress config file wp-config.php from
#        wp-config-sample.php that's shipped with WordPress.
#   2.4. Tweak wp-config.php to put in the data needed to establish
#        database connection (db name, db username and password).


body common control
{

        bundlesequence => {
                            "wordpress_install",
        };


        inputs =>          { "$(sys.libdir)/stdlib.cf" };

}



bundle agent wordpress_install
{
  vars:
      "conf[DB_HOST]"      string => "db";
      "conf[DB_NAME]"      string => "wordpress";
      "conf[DB_USER]"      string => "wordpress";
      "conf[DB_PASSWORD]"  string => "L0PSA_2011_Linux";
      "conf[DB_ROOT_PASSWORD]"  string => "Linux_2011_L0PSA";
      "conf[htmlroot]"     string => "/var/www/html";
      "conf[tarfile]"      string => "/root/wordpress-latest.tar.gz";
      "conf[wp_dir]"       string => "$(conf[htmlroot])/wordpress";
      "conf[conf]"         string => "$(conf[wp_dir])/wp-config.php";
      "conf[wp_cfgsample]" string => "$(conf[wp_dir])/wp-config-sample.php";

  methods:

    web::

      "any"
usebundle => wp_web_front_end_packages_installed("wordpress_install.conf");

      "any"
usebundle => wp_web_front_end_services_up("wordpress_install.conf");

      "any"
usebundle => wp_tarball_is_present("wordpress_install.conf");

      "any"
usebundle => wp_tarball_is_unrolled("wordpress_install.conf");

      "any"
usebundle => conf_exists("wordpress_install.conf");

      "any"
usebundle => wp_is_properly_configured("wordpress_install.conf");

    db::

      "any"
usebundle => wp_db_back_end_packages_installed("wordpress_install.conf");

      "any"
usebundle => wp_db_back_end_services_up("wordpress_install.conf");

      "any"
usebundle => wp_mysql_is_secured("wordpress_install.conf");

      "any"
usebundle => wp_mysql_configuration("wordpress_install.conf");

}


#############################################

bundle agent wp_web_front_end_packages_installed(params)
{
  vars:

    debian::
      "desired_package" slist => {
                                   "apache2",
                                   "php5",
                                   "php5-mysql",
      };

    redhat::
      "desired_package" slist => {
                                   "httpd",
                                   "php",
                                   "php-mysql",
      };
  packages:
      "$(desired_package)"
        package_policy => "add",
        package_method => generic,
        classes => if_repaired("packages_added");

  commands:
    packages_added&&redhat::
      "/sbin/service httpd graceful"
        comment => "Restarting httpd so it can pick up new modules.";

    packages_added&&debian::
      "/usr/sbin/service apache2 graceful"
        comment => "Restarting httpd so it can pick up new modules.";
}

#############################################
bundle agent wp_db_back_end_packages_installed(params)
{
  vars:

      "desired_package" slist => {
                                   "mysql-server",
      };

  packages:
      "$(desired_package)"
        package_policy => "add",
        package_method => generic,
        classes => if_repaired("packages_added");

}


#############################################

bundle agent wp_web_front_end_services_up(params)
{

  processes:
    redhat::
      "httpd"  restart_class => "start_httpd_redhat";

    ubuntu::
      "apache2"  restart_class => "start_httpd_ubuntu";


  commands:
    start_httpd_redhat::
      "/sbin/service httpd start";

    start_httpd_ubuntu::
      "/usr/sbin/service apache2 start";

}

#############################################

bundle agent wp_db_back_end_services_up(params)
{
  processes:
    redhat::
      "mysqld" restart_class => "start_mysqld_redhat";

    ubuntu::
      "mysqld" restart_class => "start_mysqld_ubuntu";


  commands:
    start_mysqld_redhat::
      "/sbin/service mysqld start";

    start_mysqld_ubuntu::
      "/usr/sbin/service mysqld start";


}

#############################################

bundle agent wp_tarball_is_present(params)
{

  classes:
      "wordpress_tarball_is_present"
        expression => fileexists("$($(params)[tarfile])");

  reports:
    wordpress_tarball_is_present::
      "WordPress tarball is on disk.";

  commands:
    !wordpress_tarball_is_present::
      "/usr/bin/wget -q -O $($(params)[tarfile]) \
                     http://wordpress.org/latest.tar.gz"
        comment => "Downloading latest version of WordPress.";
}

#############################################

bundle agent wp_tarball_is_unrolled(params)
{

  classes:
      "wordpress_directory_is_present"
        expression => fileexists("$($(params)[htmlroot])/wordpress/");

  reports:
    wordpress_directory_is_present::
      "WordPress directory is present.";

  commands:
    !wordpress_directory_is_present::
      "/bin/tar -C $($(params)[htmlroot]) -xzf $($(params)[tarfile])"
        comment => "Unrolling wordpress tarball to $($(params)[htmlroot]).";
}


#############################################

bundle agent wp_mysql_is_secured(params)
{

      #  remove the test databases and anonymous user created
      # by default and set the MySQL root password
      #
      # at first I tried to use mysql_secure_installation, but
      # it would not take the root password when it was given
      # to it as STDIN in a pipeline, it threw the error
      # "stty: standard input: Invalid argument"
      #
      # instead let's just do what mysql_secure_installation
      # does, so we can do it non-interactively:
      # - remove anonymous users
      # - remove remote root
      # - remove test database
      # - remove privileges on test database
      # - reload privilege tables



  commands:
      "/usr/bin/mysql -u root -e \"
      DELETE FROM mysql.user
      WHERE User='';
      DELETE FROM mysql.user
      WHERE User='root' AND Host!='localhost';
      DROP DATABASE test;
      DELETE FROM mysql.db WHERE Db='test' OR Db='test\\_%';
      FLUSH PRIVILEGES;\"
";

}

#############################################

bundle agent wp_mysql_configuration(params)
{

  commands:
      "/usr/bin/mysql -u root -e \"
      CREATE DATABASE IF NOT EXISTS $($(params)[DB_NAME]);
      GRANT ALL PRIVILEGES ON $($(params)[DB_NAME]).*
      TO '$($(params)[DB_USER])'@'web'
      IDENTIFIED BY '$($(params)[DB_PASSWORD])';
      FLUSH PRIVILEGES;\"
";

}

#############################################

bundle agent conf_exists(params)
{

  classes:
      "wordpress_config_file_exists"
        expression => fileexists("$($(params)[conf])");

  reports:
    wordpress_config_file_exists::
      "WordPress config file $($(params)[conf]) is present";

  commands:
    !wordpress_config_file_exists::
      "/bin/cp -p $($(params)[wp_cfgsample]) $($(params)[conf])";
}

#############################################

bundle agent wp_is_properly_configured(params)
{
  vars:
      "wpparams" slist => getindices("$(params)");

  files:
      "$($(params)[conf])"
        edit_line => replace_or_add(
          "define\('$(wpparams)', *'(?!$($(params)[$(wpparams)])).*",
          "define('$(wpparams)', '$($(params)[$(wpparams)])');"
                                   );
}

#############################################

bundle edit_line replace_or_add(pattern,line)
# Diego's.
# Replace a pattern in a file with a single line.
# If the pattern is not found, add the line to the file.
# The pattern must match the whole line (it is automatically
# anchored to the start and end of the line) to avoid
# ambiguity.

{
  replace_patterns:
      "^${pattern}$"
        replace_with => value("${line}"),
        classes => always("replace_done");

  insert_lines:
    replace_done::
      "${line}";
}


body classes always(x)
# Diego's.
# Define a class no matter what the outcome of the promise is

{
        promise_repaired => { "$(x)" };
        promise_kept => { "$(x)" };
        repair_failed => { "$(x)" };
        repair_denied => { "$(x)" };
        repair_timeout => { "$(x)" };
}




# Todo:
#
#
# MySQL:
# - submit a patch to the MySQL folks to add a non-interactive option to
#   /usr/bin/mysql_secure_installation
# - change the root password using /usr/bin/mysqladmin -u root password
#   'new-password'
# - I've hardcoded the web server name as 'web', in allowing connects. 
#   make this more flexible.  (how?)
#
# httpd:
# - instead of hardcoding "/var/www/html", derive httpd document root on
#   the fly from httpd config fileusing Function readstringlist. (If it's
#   Apache, look for DocumentRoot)
#
# iptables:
# - port 80 may be closed.  need to open it.
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-030-EC2-0220-system\_provisioning\_and\_integration\_Start\_micro\_instance.sh}
```bash, options: "linenos": true
#!/bin/sh

MY_HOST_ALIAS=$1

INSTANCE_ID=`ec2-run-instances -t t1.micro \
                               ami-7c827015 \
                               -k mysshkey 2>&1 | \
              awk '/^INSTANCE/ {print $2}'`   # CentOS image.
                                              # Use ami-6c06f305 for Ubuntu.

INSTANCE_HOSTNAME=pending

while [ "${INSTANCE_HOSTNAME}" = "pending" ]
do
	sleep 4
        INSTANCE_HOSTNAME=`ec2-describe-instances $INSTANCE_ID | \
                           awk '/^INSTANCE/ {print $4}'`


done

INSTANCE_ADDRESS=`ec2-describe-instances $INSTANCE_ID | \
                  awk '/^INSTANCE/ {print $14}'`
echo $INSTANCE_ADDRESS $MY_HOST_ALIAS $INSTANCE_ID | \
  tee -a hosts.ec2

sudo sh -c "echo $INSTANCE_ADDRESS   $MY_HOST_ALIAS  >> /etc/hosts"

sleep 70  # wait for EC2 to provision the instance

/usr/bin/ssh -t  \
             -o StrictHostKeyChecking=no \
             -i /home/user/ec2/mysshkey_key.pem \
             ec2-user@${INSTANCE_HOSTNAME} \
             sudo sh -c "\"hostname $MY_HOST_ALIAS\""



sh -c "sleep 70 \
&& \
/usr/bin/scp \
  -o StrictHostKeyChecking=no \
  -i /home/user/ec2/mysshkey_key.pem\
  /home/user/Downloads/cfengine-community-3.1.4-1.centos5.x86_64.rpm \
  /home/user/cfengine_ec2/example102_wordpress_installation.cf \
  ec2-user@${INSTANCE_HOSTNAME}: \
  && \
  /usr/bin/ssh \
    -t \
    -o StrictHostKeyChecking=no \
    -i /home/user/ec2/mysshkey_key.pem \
    ec2-user@${INSTANCE_HOSTNAME} \
    '/usr/bin/sudo rpm -i cfengine-community-3.1.4-1.centos5.x86_64.rpm \
    && rm cfengine-community-3.1.4-1.centos5.x86_64.rpm && \
    /usr/bin/sudo rsync -qa /usr/local/share/doc/cfengine/inputs/ \
                            /var/cfengine/inputs/ && \
    /usr/bin/sudo /usr/local/sbin/cf-agent;  '" 
```
\end{codelisting}

<!---
Filename: 960-040-Class\_Examples-0000-Chapter-Title.md
-->

## Advanced Usage of Classes


\begin{codelisting}
\codecaption{960-040-Class\_Examples-0240-Parsing\_readtcp\_output\_to\_set\_a\_class.cf}
```cfengine3, options: "linenos": true
# this policy runs on an haproxy load balancer
# we check a list of servers (webhosts_list)
# to tst that they are up, and if they are up,
# we make sure our haproxy configuration includes
# them.
#
# This allows us to dynamically integrate new
# Web servers into the round robin.
#
# Reference: https://cfengine.com/forum/read.php?5,19571

bundle agent load_balancer_configured_with_live_webhosts(webhosts_list)
{

  reports:
    load_balancer_hosts::
      "I am a load balancer!!";


  vars:

      "2xCRLF"
        string => "$(const.r)$(const.n)$(const.r)$(const.n)",
        comment => "HTTP requests are terminated by the double
                    CR/LF sequence";


      # variable containing HTTP response from each web server
      "my80"
        string => readtcp("$(webhosts_list)","80","GET /index.php
HTTP/1.1$(2xCRLF)Host: $(webhosts_list)$(2xCRLF)","20");


      # set server_ok class if response contains HTTP 200 OK
  classes:
      "server_ok"
        expression => regcmp(".*200 OK.*\n.*","$(my80)");


      # make sure each live (OK) web server is in the haproxy.conf

  files:

    server_ok&load_balancer_hosts::

      "/etc/haproxy.conf"
        edit_line => append_if_no_line(
          " server $(webhosts_list)
$(webhosts_list):80 maxconn 32");

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-040-Class\_Examples-0250-Set\_a\_custom\_class\_based\_on\_hostname\_pattern.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  classes:
      "italy"
        expression => classmatch("^mil.*$");  # Milan

  reports:
    italy::
      "Italy";


}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-040-Class\_Examples-0260-class-driven-configuration.cf}
```cfengine3, options: "linenos": true
# a simple, all in one file, example of configuring
# different policies per-country based on hostname naming pattern


bundle common define_global_classes {

      classes: "italy"   expression => classmatch("mil.*");

      classes: "germany" expression => classmatch("berl.*");

}




bundle agent main {

  vars:
      "country"
        slist => { "italy", "germany" };

  methods:
      "any"
        usebundle => "$(country)",
        ifvarclass => "$(country)";
}


bundle agent italy   { commands: "/bin/echo I love Milan";  }
bundle agent germany { commands: "/bin/echo I love Berlin"; }
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-040-Class\_Examples-0270-Persistent\_class.cf}
```cfengine3, options: "linenos": true
# FIXME - this example needs work

bundle agent main

{

  commands:

    ok_but_check_later::

      "/bin/echo YELLOW ALERT (condition \"ok_but_check_later\")";


  commands:

    cannot_repair_promise::

      "/bin/echo SHIELDS UP, RED ALERT";


  commands:

      "/bin/true"

        classes => set_persistent_class_based_on_promise_repair_outcome
                     (
                       "ok_but_check_later",
                       "cannot_repair_promise"
                     );


}


############################################

body classes set_persistent_class_based_on_promise_repair_outcome(if,else)

# if promise repair succeeded, set a persistent
# class for 10 minutes called "ok_but_check_later";
# else if promise repair failed, set persistent class
# "cannot_repair_promise_DANGER_DANGER".

{
        promise_repaired => { "$(if)" };
        repair_failed => { "$(else)" };
        persist_time => "10"; # in minutes
}
##########################################################################

# Here is an example of how you might use a persistent class
#
# 1. You detect a rootkit from some filesearch and you want
# to delete it immediately, but the danger is maybe not over.
#
# 2. You define persistent class for the next hour DEFCON1
# which activates repeated scans of the filesystem looking
# for trouble as you suspect you might be under attack.
```
\end{codelisting}

<!---
Filename: 960-045-Versioning\_Policy-0000-Chapter-Title.md
-->

## Versioning Policy

Here are examples of versioning your policies and integrating CFEngine with a Version Control System.


\begin{codelisting}
\codecaption{960-045-Versioning\_Policy-1040-Version\_number\_Plain.cf}
```cfengine3, options: "linenos": true
body common control
{
        version => "1.1";
        bundlesequence => { "example" };
}


########################################


bundle agent main
{

  commands:

      "/bin/nosuchcommand hello world, i love wednesdays and coffee";
}
```
\end{codelisting}

<!---
Filename: 960-080-Special\_Notes-0000-Chapter-Title.md
-->

## Special Notes And Gotchas


\begin{codelisting}
\codecaption{960-080-Special\_Notes-0640-Iteration\_over\_a\_global\_list\_Using\_parameterization.cf}
```cfengine3, options: "linenos": true
# Scalar references to *local* list variables imply iteration.
# To iterate over a global list variable, map the global list
# into the local context, or supply it to the bundle as a
# parameter.
#
# Example of mapping it into the local context

body common control {
        bundlesequence => { runme(@(g.myusers)) };  # note lack of
      # " symbols
}

bundle common g
{
      vars: "myusers"  slist => { "joe", "mary", "ann" };

}


bundle agent runme(x)

{

  reports:

      "$(x)";
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-080-Special\_Notes-0650-Iteration\_over\_a\_global\_list\_Direct\_method.cf}
```cfengine3, options: "linenos": true
# Scalar references to *local* list variables imply iteration.
# To iterate over a global list variable, map the global list
# into the local context.  There are two ways to do it, this
# is the direct method.



bundle common g
{
      vars: "myusers"  slist => { "joe", "mary", "ann" };

}


bundle agent main

{
      vars: "mylist" slist => { @(g.myusers) };

  reports:

      "$(mylist)";
}
```
\end{codelisting}

<!---
Filename: 960-080-Special\_Notes-0660-Max\_scalar\_list\_and\_array\_sizes.md
-->

TIP: There is no limit to the length of lists or arrays, but there is a limit to the size of variable-expanded strings (scalars). The final result of any single variable expansion is limited to about 4k.



<!---
Filename: 960-080-Special\_Notes-0670-Unit\_tests.md
-->

'/usr/local/share/doc/cfengine/' contains over 220 examples (originally unit tests).

They don't all work, but most do.

Potentially useful in learning CFEngine.



<!---
Filename: 960-080-Special\_Notes-0680-Orion\_Cloud\_Pack.md
-->

### Orion Cloud Pack - library for EC2


### Essential Files  
```text
promises.cf            Main configuration file.
update.cf              Update configuration.
failsafe.cf            Failsafe configuration.
cfengine_stdlib.cf     CFEngine standard library.
```



### Maintenance Examples
```text
change_mgt.cf          Implement security tripwire on files/directories.
ensure_ownership.cf    Home directory ownership and permission maintenance.
fix_broken_software.cf Package installation and permission correction.
garbage_collection.cf  Log rotation and removal.
harden_xinetd.cf       Disable xinetd services specified.
iptables.cf            Secure system with sysctl.conf and iptables.
name_resolution.cf     Edit /etc/resolv.conf to the specified DNS servers
```


### System Setup Examples 
```text
c_cpp_env.cf           Set up C programming environment.
db_mysql.cf            Install and run MySQL
db_postgresql.cf       Install and run PostgreSQL
db_sqllite.cf          Install and run SQLlite
jboss_server.cf        Prepare JAVA environment and run JBOSS.
nagios.cf              Setup NAGIOS monitoring node.
nginx_perlcgi.cf       Setup NGINX webserver perlCGI.
nginx_phpcgi.cf        Setup NGINX webserver phpCGI.
ntp.cf                 Setup NTP server and clients.
perl_env.cf            PERL programming language install.
php_webserver.cf       Setup a PHP webserver.
python_env.cf          PYTHON programming install.
ruby_env.cf            Setup ruby on rails environment.
sshd_conf.cf           Ensure sshd config is correct.
tomcat_server.cf       Setup a tomcat server.
varnish.cf             Set up Varnish web accelerator
```



<!---
Filename: 960-080-Special\_Notes-0690-Always\_specify\_the\_class.md
-->

TIP: Always specify the class , or else you may inadvertently inherit the class specification from an earlier promise


\begin{codelisting}
\codecaption{960-080-Special\_Notes-0700-Be\_careful\_with\_class.cf}
```cfengine3, options: "linenos": true
bundle agent main {
  commands:
    customclass::
      "/bin/echo customclass is set";
      "/bin/echo this is always true";
}

# run cf-agent on this policy with and without -Dcustomclass
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-080-Special\_Notes-0710-No\_safeguard\_for\_syntactically\_correct\_but\_insane\_policy.cf}
```cfengine3, options: "linenos": true
# a mutually exclusive configuration

body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

############################################################


bundle agent main {

  files:

      "/tmp/plug"
        delete => tidy;

  files:

      "/tmp/plug"
        create => "true";
}
```
\end{codelisting}

<!---
Filename: 960-090-Packages-0000-Chapter-Title.md
-->

## Packages



<!---
Filename: 960-090-Packages-0740-A\_note\_on\_versioning.md
-->

TIP: Package versions are of data type string, not number!  Thus numeric comparison, while it can be attempted, is fraught with peril and frustration.

Which version is newer?

* 1.2.3f
* 1.2.3-4
* 1.2.3-hotpotato 

NOTE: See http://semver.org/ for a proposal for a meaningful versioning standard.


\begin{codelisting}
\codecaption{960-090-Packages-0750-install\_packages\_from\_local\_filesystem\_based\_repository.cf}
```cfengine3, options: "linenos": true
# NOTE: This policy assumes pre-3.7 packages promises.
# Packages promises were completely rewritten in 3.7
# They still work but are deprecated.

body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  packages:
      "epel-release"
        package_policy => "add",
        package_version => "5-4",
        package_architectures => { "noarch" },
        package_method => rpm_filebased("/repo/RPMs");
}

body package_method rpm_filebased(path)
# Contributed by Aleksey Tsalolikhin. Written on 29-Feb-2012.
# Based on yum_rpm body in COPBL by Trond Hasle Amundsen.
# Intended to install packages from local package repository.
# You must specify the path to the local package repository as the argument.

{
        package_file_repositories => { "$(path)" };
# the above is an addition to Trond's yum_rpm body

        package_add_command    => "/bin/rpm -ihv ";
# The above is a change from Trond's yum_rpm body, this makes the commands
# rpm only.
#
# The reason I changed the install command from yum to rpm is yum will by
# default refuse to install the epel-release RPM as it does not have the
# EPEL GPG key, but rpm goes ahead and installs the epel-release RPM and
# the EPEL GPG key.

        package_name_convention => "$(name)-$(version).$(arch).rpm";
# The above is a change from Tron's yum_rpm body. When
# package_file_repositories is in play, package_name_convention has
# to match the file name, not the package name, per the CFEngine 3
# Reference Manual.
# 

# The rest is unchanged from Trond's yum_rpm body
        package_changes => "bulk";
        package_list_command => "/bin/rpm -qa --qf \
  '%{name} %{version}-%{release} %{arch}\n'";

        package_list_name_regex    => "^(\S+?)\s\S+?\s\S+$";
        package_list_version_regex => "^\S+?\s(\S+?)\s\S+$";
        package_list_arch_regex    => "^\S+?\s\S+?\s(\S+)$";

        package_installed_regex => ".*";


        package_delete_command => "/bin/rpm -e --allmatches";
        package_verify_command => "/bin/rpm -V";
}


# Example output from running this policy:
# linux# cf-agent -f \
# ./MISC_install_packages_from_local_filesystem_based_repository.cf \
#  -b example -KI
# >> Using command line specified bundlesequence
#
# Q:rpm -ihv  "/repo/RPM ...:Preparing...   \
#             ##################################################
# Q:rpm -ihv  "/repo/RPM ...:epel-release   \
#            ##################################################
#
# Q:rpm -ihv  "/repo/RPM ...:
# linux#

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-090-Packages-0760-install\_RPM\_from\_a\_local\_directory.cf}
```cfengine3, options: "linenos": true
body common control {
        bundlesequence => { "commands__install_PGDG_yum_repo_RPM" };
}


bundle agent commands__install_PGDG_yum_repo_RPM {

  packages:

      "pgdg-centos"

        package_policy => "add",
        package_method => yum_filebased;

}


body package_method yum_filebased
{
        package_file_repositories => { "/repo" };   
        # A list of machine-local directories to search for packages

        package_changes => "bulk";
        package_list_command => "/usr/bin/yum list installed";

      # Remember to escape special characters like |

        package_list_name_regex    => "([^.]+).*";
        package_list_version_regex => "[^\s]\s+([^\s]+).*";
        package_list_arch_regex    => "[^.]+\.([^\s]+).*";

        package_installed_regex => ".*installed.*";
        package_name_convention => "$(name).$(arch)";

        package_add_command => "/usr/bin/yum -y install";
        package_delete_command => "/bin/rpm -e";
        package_verify_command => "/bin/rpm -V";
}

```
\end{codelisting}

<!---
Filename: 960-100-Classes-0000-Chapter-Title.md
-->

## Advanced Usage Of Classes


\begin{codelisting}
\codecaption{960-100-Classes-0780-setting\_multiple\_classes\_as\_a\_result\_of\_a\_single\_promise.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  vars:
      "slist_of_classes" slist => { "class1", "class2" };

  files:
      "/etc/httpd/conf/httpd.conf"
        edit_line => insert_lines("#test comment"),
        classes  => if_repaired_set_these_classes("@(main.slist_of_classes)");

  reports:
      class1:: "class1";
      class2:: "class2";

}

body classes if_repaired_set_these_classes(list)
{
        promise_repaired => { "@(list)" };
}

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-100-Classes-0790-Return\_codes.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  commands:

      "/bin/false"

        classes => cmd_kept("1","ok");

  reports:
    ok::

      "Command completed successfully";

}




body classes cmd_kept(code,class)
{
        repaired_returncodes => { "$(code)" };
        promise_repaired => { "$(class)" };
}


```
\end{codelisting}
\begin{codelisting}
\codecaption{960-100-Classes-0800-returncodes.cf}
```cfengine3, options: "linenos": true
# customize CFEngine's idea of promise kept, returned or failed
# based on command's return code.
#
# For this demo, add an account "joe" and then use
# userdel to remove it.
#
# Run "chattr +i /etc/passwd" after adding the "joe" account
# to induce a failure to remove Joe.
#

bundle agent main {

  commands:

      "/usr/sbin/userdel"
        args => "joe",
        comment => "We don't want joe on our systems ever again.",
        classes => customized_for_userdel;

  reports:

      joe_removed::   "Joe was removed";
      no_joe::        "Don't worry, there is no account for Joe.";
      (!joe_removed)&(!no_joe)::  "!!!!  Joe is still here.  !!!!";
}

body classes customized_for_userdel {

        kept_returncodes => { "6" };   # user was not present in the file
        repaired_returncodes => { "0" }; # user was successfully removed
        promise_repaired => { "joe_removed" };
        promise_kept => { "no_joe" };
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-100-Classes-0810-Canonifying\_variables\_to\_use\_them\_as\_class\_names.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  vars:
      "myarray[loc@t!on]" string  => "Bloomington";
      "myarray[t!me###]"     string  => "first week of April";


      "index" slist =>  getindices("myarray");


      "cindex[$(index)]" string => canonify("$(index)");


  reports:



      "Original keys: $(index)";
      "Canonified keys: $(cindex[$(index)])";
}
```
\end{codelisting}

<!---
Filename: 960-100-Classes-0820-Canonifying\_variables\_to\_use\_them\_as\_class\_names.md
-->

### Data Structures:

### Array myarray
```text
loc@t!on,Bloomington
t!me###,first week of April
```

### List index
```text
loc@t!on
t!me###
```

### Array cindex
```text
loc@t!on,loc_t_on
t!me###,t_me___
```


\begin{codelisting}
\codecaption{960-100-Classes-0830-ifvarclass.cf}
```cfengine3, options: "linenos": true
bundle agent main {


      vars:  "fruit"  string =>  "banana";

  reports:



      "I love bananas for breakfast"

        ifvarclass => "$(fruit)";

}

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-100-Classes-0840-non\_persistent\_class.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


bundle agent main {


  files:
      "/tmp/file.txt"
        create => "true",
        edit_line => insert_lines("hello world 1234"),
        classes => if_repaired("promise_repaired");


  reports:
    promise_repaired::
      "soft class is set";

}


```
\end{codelisting}

<!---
Filename: 960-110-Regular\_Expressions-0000-Chapter-Title.md
-->

## Advanced Usage Of Regular Expressions


\begin{codelisting}
\codecaption{960-110-Regular\_Expressions-0860-Backreferences.pl}
```perl, options: "linenos": true
#!/usr/bin/env perl

$record =
"James Alexander Richard Smith";

if ( $record =~ /^(.*?) (.*) (.*)$/ ) {

  print "First name: $1\n";
  print "Middle name(s): $2\n";
  print "Last name: $3\n";
}

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-110-Regular\_Expressions-0870-commentinging\_out\_file\_content.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


bundle agent main {

  files:

      "/tmp/testfile"

        edit_line => comment_out_everything;
}


bundle edit_line comment_out_everything {

  replace_patterns:

      "^([^#].*)"

        replace_with => comment("#");

}

```
\end{codelisting}
\begin{codelisting}
\codecaption{960-110-Regular\_Expressions-0880-replace\_patterns.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  files:

      "/tmp/data.txt"
        edit_line => change_dogs_to_cats;

}


bundle edit_line change_dogs_to_cats {

  replace_patterns:

      "dog"

        replace_with => value("cat");

}
```
\end{codelisting}

<!---
Filename: 960-110-Regular\_Expressions-0890-Inverse\_match.md
-->

Let's say you want to write a regex that will match any string that does NOT contain the string "hello world". Use:

^((?!hello world).)*$

This is explained in http://stackoverflow.com/questions/406230/regular-expression-to-match-string-not-containing-a-word



<!---
Filename: 960-130-Commands-0000-Chapter-Title.md
-->

## Advanced Usage Of Commands


\begin{codelisting}
\codecaption{960-130-Commands-0960-ifelapsed.cf}
```cfengine3, options: "linenos": true
# do not use -K switch when running this example!!
#
# Run it in verbose mode and grep the output for "elapsed"

bundle agent main {

  commands:

      "/bin/echo /bin/cycle_shield_frequencies.sh"

        action => every_2_minutes;
}

body action every_2_minutes
{
        ifelapsed => "2";  # in minutes
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-130-Commands-0970-Getting\_shell\_to\_interpolate\_a\_shell\_variable\_requires\_useshell.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  commands:

      "/bin/echo"
        args => " \"hello $(const.dollar){LOGNAME} $(const.t)adfs\"",
        contain => in_shell;

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-130-Commands-0980-contain\_preview.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  commands:

      "/bin/touch /tmp/test2"

        contain => preview;

}

body contain preview {

        preview => "true";

}
```
\end{codelisting}

<!---
Filename: 960-140-Linking\_Promises\_with\_Classes-0000-Chapter-Title.md
-->

## Advanced Usage Of Classes: Linking Promises


\begin{codelisting}
\codecaption{960-140-Linking\_Promises\_with\_Classes-1000-if\_repaired.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  files:
      "/tmp/httpd.conf"
        create => "true",
        edit_line => insert_lines("ServerName localhost"),
        classes => if_repaired("httpd_restart_needed");


  commands:
    httpd_restart_needed::
      "/etc/init.d/httpd reload";


}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-140-Linking\_Promises\_with\_Classes-1010-if\_repaired\_stop\_cups\_and\_complain.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


bundle agent main {

  processes:

      "cupsd"

        process_stop => "/etc/init.d/cups stop",
        comment => "We don't want print services on our Web servers.",
        classes => if_repaired("complain_loudly_about_cups");

  commands:
    complain_loudly_about_cups::
      "/bin/echo send up a flare about CUPS";
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-140-Linking\_Promises\_with\_Classes-1020-Verbose\_logging\_of\_repairs.cf}
```cfengine3, options: "linenos": true
bundle agent main
{
  files:

      "/tmp/newfile3"

        handle => "newfile3_exists",
        create => "true",
        action => log("Created very important file $(this.promiser)");
}

body action log(msg)
{
        log_string => "$(sys.date) \
$(this.promise_filename):$(this.promise_linenumber) \
$(this.handle): $(msg)";
        log_repaired => "stdout";
}

# Logs a message like:
#    L: Mon Oct 19 18:05:44 2015 \
# /home/aleksey/cfengine_tutorial/chapters/\
# 790-140-Linking_Promises_with_Classes-1020\
# -Verbose_logging_of_repairs.cf:5 \
# newfile3_exists: Created very important file /tmp/newfile3
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-140-Linking\_Promises\_with\_Classes-1030-edit\_crontab\_and\_HUP\_crond.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


bundle agent main {

  files:

      "/var/spool/cron/root"

        edit_line => cf_execd_entry_is_present,
        create => "true",
        classes => if_repaired("restart_crond");

  processes:
    restart_crond::
      "crond"
        signals => { "hup" };


}


bundle edit_line cf_execd_entry_is_present {

  insert_lines:
      "5,10,15,20,25,30,35,40,45,50,55 * * * * /var/cfengine/bin/cf-execd -F";
}

```
\end{codelisting}

<!---
Filename: 960-150-Dynamic\_Bundlesequence-0000-Chapter-Title.md
-->

## Dynamic Inputs And Bundlesequence


\begin{codelisting}
\codecaption{960-150-Dynamic\_Bundlesequence-1050-Activate\_a\_class\_if\_it\_is\_appropriate\_for\_my\_context.cf}
```cfengine3, options: "linenos": true
# I want to target a promise to a certain group of servers.
# However I want to abstract the elements of that group from
# the promises that target that group, so that when I add an
# element to that group, I only need to update *one* promise,
# the one enumerating that group.
#
# The following policy will report "I am a webserver" if its
# hostname is listed in "webservers" slist.

bundle common global_vars {

  vars:
      "webservers"
        slist => { "web01", "web02", "web03" };

}


bundle common global_classes {

  classes:

      "webfarm"
        expression => reglist(
                                "@(global_vars.webservers)",
                                 escape("$(sys.host)")
                             );

}

bundle agent main {

  reports:

    webfarm::

      "I am a web server";

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-150-Dynamic\_Bundlesequence-1060-Jeff\_Blaine\_dynamic\_bundlesequence\_with\_parameterized\_mybundle.cf}
```cfengine3, options: "linenos": true
# by Jeff Blaine

# Changes the order of NTP servers in ntp.conf based on site (using class)
# if we're in site x, site X servers should be first; if we're in site y,
# site y servers should be first.

# Example run:
#    [cfengine00  practical_examples]# cf-agent -KI \
#      -f ./MISC_dynamic_bundlesequence_with_parameterized_mybundle.cf \
#       -Dsite_x
#     -> Edited file /etc/ntp.conf
#    [cfengine00  practical_examples]# cat /etc/ntp.conf
#    fudge   127.127.1.0 stratum 10
#    server  127.127.1.0
#    # will be destroyed by CFEngine, so don't do that.
#    # This file is configured by CFEngine.  Manual edits to this file
#    server ntp-sitex.our.org
#    server ntp-sitey.our.org
#    restrict -4 default kod notrap nomodify nopeer noquery
#    restrict -6 default kod notrap nomodify nopeer noquery
#    [cfengine00  practical_examples]# cf-agent -KI \
#      -f ./MISC_dynamic_bundlesequence_with_parameterized_mybundle.cf \
#      -Dsite_y
#     -> Edited file /etc/ntp.conf
#    [cfengine00  practical_examples]# cat /etc/ntp.conf
#    fudge   127.127.1.0 stratum 10
#    server  127.127.1.0
#    # will be destroyed by CFEngine, so don't do that.
#    # This file is configured by CFEngine.  Manual edits to this file
#    server ntp-sitey.our.org
#    server ntp-sitex.our.org
#    restrict -4 default kod notrap nomodify nopeer noquery
#    restrict -6 default kod notrap nomodify nopeer noquery
#    [cfengine00  practical_examples]#
#



bundle common g
{
      # Define NTP servers in a specific order per site.

      # You could define everything here in "main" and change the references
      # there in "methods:" if you wanted to.

  vars:

    site_x::
      "ntpservers" slist => {
                              "ntp-sitex.our.org",
                              "ntp-sitey.our.org",
      };

    site_y::
      "ntpservers" slist => {
                              "ntp-sitey.our.org",
                              "ntp-sitex.our.org",
      };
}

bundle agent main
{
  methods:

    site_x::
      "site_x" usebundle =>
      system_ntpclient_configure(@(g.ntpservers));

    site_y::
      "site_y" usebundle =>
      system_ntpclient_configure(@(g.ntpservers));
}

body common control
{
        bundlesequence => {
                            "main"
        };

      # Building a per-site inputs list is beyond the scope of this
      # example.
        inputs => {
                    "$(sys.libdir)/stdlib.cf",
                    "ntp.cf"
        };
}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-150-Dynamic\_Bundlesequence-1070-Jeff\_Blaine\_ntp.cf}
```cfengine3, options: "linenos": true
#Author: Jeff Blaine

#
# Given a list of servers, establish a basic NTP configuration file
# containing that list of servers as well as a set of "restrict"
# lines based on OS (some OSes don't support all modern options
# to the restrict directive).
#
# EXERCISE: augment the following (with new bundle(s) as needed)
#           to ensure that the appropriate NTP package(s) are
#           installed on the host, per OS.  Make this bundle
#           below depend on the package being installed first.
#
# EXERCISE: augment the following to ensure that the NTP client
#           is running.  This new logic should depend on the client
#           package(s) being installed per the exercise above.
#
bundle agent system_ntpclient_configure(servers)
{
  vars:

    solaris::

      "configfile" string => "/etc/inet/ntp.conf";

      # SunOS5.10 (at least) does not support 'kod' or '-6' like Linux
      "restrictlines"
        slist => { "restrict default notrap nomodify nopeer noquery" };

    redhat|centos::

      "configfile" string => "/etc/ntp.conf";

      "restrictlines"
        slist => {
                   "restrict -4 default kod notrap nomodify nopeer noquery",
                   "restrict -6 default kod notrap nomodify nopeer noquery",
                 };

  files:

    redhat|centos|solaris::

      "$(configfile)"
        edit_line =>
      ntpclient_config_edit(@(system_ntpclient_configure.servers),
                            @(system_ntpclient_configure.restrictlines));
}

bundle edit_line ntpclient_config_edit(servers, restrictlines)
{
  delete_lines:
      ".*";

  insert_lines:
      # Add our static content first (4 lines).
      "# This file is configured by CFEngine.  Manual edits to this file
# will be destroyed by CFEngine, so don't do that.
server  127.127.1.0
fudge   127.127.1.0 stratum 10"
        insert_type => "preserve_block",
        location => start;

      # ^^^^ There is a bug in 3.2.0 (at least) that will cause the
      # above promise definition to not keep the proper order of lines.
      # Just be aware.  In this case, it just makes a silly looking file
      # that still functions properly as far as NTP is concerned.

      # Add our NTP servers, one per line
      "server $(servers)";

      # Add our restrict rules, one per line
      "$(restrictlines)";
}
```
\end{codelisting}

<!---
Filename: 960-160-Databases-0000-Chapter-Title.md
-->

## Promises Concerning Databases

Demonstrate CFEngine integration with PostgreSQL.


\begin{codelisting}
\codecaption{960-160-Databases-1090-clean\_slate\_for\_database\_demo.sh}
```bash, options: "linenos": true
#!/bin/sh

yum -y remove postgresql postgresql-server
rm -rf /var/lib/pgsql
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-160-Databases-1100-db\_demo.cf}
```cfengine3, options: "linenos": true
# Demonstration of CFEngine's databases promises.
# First, install and configure a PostgreSQL database
# cluster and create an database.
# Then use "databases" type promises to set up and
# maintain the schema of 3 tables.
#
# Note: package_list_update_ifelapsed should be set to 0
# for demoes.
#
# Demoes: - self-heal from database cluster shutdown
#         - self-heal from dropping a table
#         - self-heal from dropping a table column
#         - self-heal from changes to pg_hba.conf





body common control {

        version => "1.1 21-Oct-2011";

        host_licenses_paid => "10";

        inputs => { "$(sys.libdir)/stdlib.cf" };

        bundlesequence => {
                             "db_cluster_is_installed",

                             "pg_hba_conf_trusts_local_users",

                             "db_cluster_is_running",

                             "database_exists",

                             "schema_exists_and_is_correct",

                           };

}

################################################

bundle agent db_cluster_is_installed {

  packages:


      "postgresql-server"

        package_policy => "add",
        package_method => yum,
        classes => if_repaired("start_postgres");

      "postgresql"
        package_policy => "add",
        package_method => yum;


  commands:

    start_postgres::

      "/sbin/service postgresql start";


}

################################################

bundle agent pg_hba_conf_trusts_local_users {


  files:
      "/var/lib/pgsql/data/pg_hba.conf"

      # this is a regular comment

        edit_line => trust_local_users,
        comment => "Allow root to access the DB cluster
                    so CFEngine can set up the database
                    and table schema",
      # the above was a Knowledge Management comment
        classes => if_repaired("reload_postgres");

  commands:

    reload_postgres::

      "/sbin/service postgresql reload";


}


################################################


bundle agent db_cluster_is_running {


  processes:

      "postgres"

        restart_class => "start_postgres";


  commands:

    start_postgres::

      "/sbin/service postgresql start";

}




################################################

bundle agent database_exists {

  commands:

      "/usr/bin/createdb -U postgres conference \
         >/dev/null 2>/dev/null"
        contain => in_shell;

}


################################################


bundle agent schema_exists_and_is_correct {

  vars:
      "create_and_verify"
        slist => { "create", "verify" };


  databases:


      "conference/speakers"

        database_operation => "$(create_and_verify)",
        database_type => "sql",
        database_columns => {
                              "speaker_name,varchar,50",
                              "speaker_bio,varchar,600",
                              "speaker_affiliation,varchar,50",
        },
        database_server => demo_postgres_server;


      "conference/rooms"

        database_operation => "$(create_and_verify)",
        database_type => "sql",
        database_columns => {
                              "room_name,varchar,256",
                              "room_number_of_seats,integer",
        },
        database_server => demo_postgres_server;


      "conference/talks"

        database_operation => "$(create_and_verify)",
        database_type => "sql",
        database_columns => {
                              "speaker_name,varchar,256",
                              "room_name,varchar,256",
                              "start_time,date",
        },
        database_server => demo_postgres_server;


}


################################################


body database_server demo_postgres_server {

        db_server_owner => "postgres";

        db_server_password => "";

        db_server_host => "localhost";

        db_server_type => "postgres";

        db_server_connection_db => "postgres";

}

################################################

bundle edit_line trust_local_users {

      delete_lines: ".*";

      insert_lines: "
# !!! This file is under CFEngine control.  Do not edit
# it directly or your changes may be overwritten.
#
# TYPE  DATABASE    USER        CIDR-ADDRESS          METHOD
local   all         all                               trust
";

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-160-Databases-1110-Create\_DB\_Users.cf}
```cfengine3, options: "linenos": true
body common control {

        bundlesequence => { "create_db_users" };
        inputs => { "$(sys.libdir)/stdlib.cf" };

}


########################################################

bundle common db {

  vars:

      "db_users"

        slist   => splitstring(execresult("/usr/bin/psql -AXqt \
                     -c 'select usename from pg_user' -U postgres",
                                           "noshell"
                                         ),
                               "\n",
                               "100"
                              ),
        comment => "List of DB users";


      "createuser_defaults"
        string => " -U postgres  --no-createdb \
                    --no-createrole --no-superuser ",
        comment => "Default arguments we'll use with /usr/bin/createuser
                    to create regular unprivileged PostgreSQL accounts";

}

########################################################


bundle agent create_db_users {

  classes:

      "postgres_node"

        expression => returnszero("/usr/bin/pgrep postmaster >/dev/null",
                                  "useshell"),
        comment => "Identify if this node is running postgres.";


  methods:

    postgres_node::

      "any"
        usebundle => create_pg_user("nagios", "$(db.createuser_defaults)"),
        comment => "Every node that runs postgres should have pg user nagios
                    for monitoring using check-postgres.pl plugin";

    specialcase1::

      "any"
        usebundle => create_pg_user("superuser1",
                                    " -U postgres --superuser "),
        comment => "Create db superuser superuser1";

    specialcase2::

      "any"
        usebundle => create_pg_user("regularuser1",
                                    "$(db.createuser_defaults)"),
        comment => "Application X requires regularuser1";


}


####################################################

bundle agent create_pg_user(username,args) {


  classes:

      "$(username)_exists"

        expression => reglist("@(db.db_users)","$(username)"),
        comment => "Check if username already exists in the database.";


  commands:


      "/usr/bin/createuser $(args)  $(username)"
        contain => in_shell_and_silent,
        ifvarclass => "!$(username)_exists",
        comment => "Create PostgreSQL user $(username) with
                    createuser args $(args)";
}
```
\end{codelisting}

<!---
Filename: 960-310-Processes-0010-title\_card.md
-->

## Making Sure A Process Is Running


\begin{codelisting}
\codecaption{960-320-Processes-0020-Restart\_a\_process\_if\_it\_is\_running\_or\_start\_it\_if\_it\_is\_not\_running.cf}
```cfengine3, options: "linenos": true
bundle agent aingent main
{
  processes:

      ".*"

        process_count   => anyprocs,
        process_select  => proc_finder;


  commands:

    process_running::

      "/bin/echo restart command";

    process_not_running::
      "/bin/echo start command";

}

########################################################

body process_select proc_finder

{

        command => "sendmail: .*";
        # (Anchored) regular expression matching the CMD
        # field of a process

        process_result => "command";

}

########################################################

body process_count anyprocs

{
        match_range => "0,0";
        # Integer range for acceptable number of matches for this process

        out_of_range_define => { "process_running" };
        # List of classes to define if the matches are out of range

        in_range_define => { "process_not_running" };
        # List of classes to define if the matches are in range.

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-330-Files-0030-perms\_groups.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  files:

      "/tmp/testfile"

        perms => acceptable_groups;
}

body perms acceptable_groups {

        groups => {"root", "games", "mail" };

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-330-Files-0040-rename.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


bundle agent main {

  files:
      "/bin/chown"
        rename => to("/bin/CHOWN");
}

```
\end{codelisting}

<!---
Filename: 960-330-Files-0050-title\_card.md
-->

###  Files


\begin{codelisting}
\codecaption{960-330-Files-0060-Disable\_And\_Rename.cf}
```cfengine3, options: "linenos": true
#body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  files:
      "/tmp/test2"
        rename => disable_for_good;
}


body rename disable_for_good
{
        disable => "true";
        disable_mode => "000";
}


```
\end{codelisting}
\begin{codelisting}
\codecaption{960-330-Files-0070-Repository.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }

bundle agent main {

  files:

      "/tmp/file.txt"

        create => "true",
        edit_line => insert_lines("$(sys.date)"), # guarantees an edit
        edit_defaults => timestamp,
        repository => "/var/cfengine/repository",
        comment => "Save all history of edits to this important file.";
}

body edit_defaults timestamp
{
        edit_backup => "timestamp";
}

# Example output:
#
# linux# cf-agent -f ./MISC__files__repository.cf -b example -KI
#  >> Using command line specified bundlesequence
#  -> Edited file /tmp/file.txt
# Moved /tmp/file.txt_1333404966_Mon_Apr__2_17_16_07_2012.cf-before-edit \
# to repository location \
# /var/cfengine/repository/_tmp_file.txt_1333404966_Mon_Apr__2_17_16_07_2012.\
# cf-before-edit
# linux# cf-agent -f ./MISC__files__repository.cf -b example -KI
#  >> Using command line specified bundlesequence
#  -> Edited file /tmp/file.txt
# Moved /tmp/file.txt_1333404969_Mon_Apr__2_17_16_10_2012.cf-before-edit \
# to repository location \
# /var/cfengine/repository/_tmp_file.txt_1333404969_Mon_Apr__2_17_16_10_2012.\
# cf-before-edit
# linux#
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-330-Files-0080-edit\_multiple\_files.cf}
```cfengine3, options: "linenos": true
bundle agent main {

      # Demonstrate using regex to edit multiple files

  files:

      "/tmp/etc/.*.conf"

        edit_line => has_my_name_in_it,
        pathtype => "regex",
        comment => "Every *.conf file in /etc/ should have my name in it.";

}


bundle edit_line has_my_name_in_it {

      insert_lines: "# This file belongs to by Aleksey Tsalolikhin.";

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-330-Files-0090-input\_type\_preserving\_order\_while\_editing\_a\_file.cf}
```cfengine3, options: "linenos": true
body file control { inputs => { "$(sys.libdir)/stdlib.cf" }; }


bundle agent main {

  files:

      "/tmp/testfile"

        create  => "true",
        edit_line => proper_greetings;
}


####################################################


bundle edit_line  proper_greetings {


  insert_lines:

      "Good Evening"
        location => after("Good Day");

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-330-Files-0100-owners.cf}
```cfengine3, options: "linenos": true
bundle agent main {

  files:

      "/tmp/testfile"

        comment => "/tmp/testfile must be mode 612 for application X to work;
                    it must be owned by user aleksey and group cfengine",
        create  => "true",
        perms   => proper_owner("aleksey");

}

############################################################

body perms proper_owner(user)
{
        owners => { "$(user)", "rob", "user2" };
}

```
\end{codelisting}

<!---
Filename: 960-340-PXEboot\_Kickstart\_Server-0110-title\_card.md
-->

## PXEboot Kickstart Server


\begin{codelisting}
\codecaption{960-340-PXEboot\_Kickstart\_Server-0120-Server.cf}
```cfengine3, options: "linenos": true
# configure a system to be a pxeboot kickstart server
# and to serve CentOS 5.7 i386.  configure kickstart
# config file to bootstrap CFEngine onto the new system:
# download and install CFEngine RPM, and download,
# install and execute CFEngine policy set.

# assumes that contents of CentOS 5.7 i386 installation DVD
# are in the Apache document root, /var/www/html/centos-5.7-i386
# (TODO - make a promise to mirror CentOS to this directory
# as per http://drcs.ca/blog/how-to-mirror-centos-5-and-\
# use-it-as-a-local-yum-repository/ )
#
#
# assumes the pxeboot/kickstart server address is 192.168.1.1
#
# WARNING: lowers the firewall instead of poking holes for
# UDP 67 and 69 (bootp and tftp)
# (TODO: poke holes for UDP 67 and 69 instead of lowering firewall)
#
# Assumes CFEngine RPM cfengine-community-3.2.1-1.el5.i386.rpm
# is in /var/www/html # (this needs to be done manually as
# cfengine.com requires login to access RPMs)
#
# Assumes CFengine policy files are in the httpd document root,
# cfengine_inputs.tar

body common control {

        inputs => { "$(sys.libdir)/stdlib.cf" };

        bundlesequence => {
                            "packages",
                            "enable_services",
                            "configure_dhcpd_config_file",
                            "run_pxe_commands_to_setup_pxeboot",
                            "start_services",
                            "configure_firewall_to_allow_bootp_and_tftp",
                            # not really.  i just turn off the firewall.
                            "configure_kickstart_file",
        };
}

bundle agent packages {


  vars:
      "desired_packages"
      ######################################################
      #              START OF PACKAGE LIST                 #

        slist =>
      {
        "system-config-netboot",
        "httpd",
        "xinetd",
        "tftp",
        "dhcp",
      };

      #              END OF PACKAGE LIST                   #
      ######################################################

  packages:

      "$(desired_packages)"

        package_method => yum,
        package_policy => "add";


}

bundle agent enable_services {

      # make sure services are configured to start at boot

  commands:
      "/sbin/chkconfig xinetd on";
      "/sbin/chkconfig tftp on";
      "/sbin/chkconfig httpd on";
      "/sbin/chkconfig dhcpd on";

}



bundle agent configure_dhcpd_config_file {

  files:
      "/etc/dhcpd.conf"
        create => "true",
        edit_line => my_dhcpd_config;
}

bundle edit_line my_dhcpd_config {

      delete_lines: ".*";

  insert_lines:

      "allow booting;
allow bootp;
class \"pxeclients\" {match if substring(option vendor-class-identifier, \
0, 9) = \"PXEClient\"; next-server 192.168.1.1; \
filename \"linux-install/pxelinux.0\"; }
ddns-update-style ad-hoc;

subnet 192.168.0.0 netmask 255.255.0.0 {
range 192.168.1.2 192.168.1.254;
}
"
        insert_type => "preserve_block";

}


bundle agent start_services {

      # make sure services are configured to start at boot

  commands:
      "/etc/init.d/httpd start";
      "/etc/init.d/xinetd start";
      "/etc/init.d/dhcpd start";
}

bundle agent configure_firewall_to_allow_bootp_and_tftp {
      # this bundle should edit iptables to allow UDP 67 and 69
  commands:
      "/etc/init.d/iptables stop";  # quick and dirty, not safe
}


bundle agent configure_kickstart_file {

  files:
      "/var/www/html/centos-5.7-i386.ks"

        create => "true",
        edit_line => my_kickstart_file;
}

bundle edit_line my_kickstart_file {


      delete_lines: ".*";

  insert_lines:
      "
cmdline
install
url --url http://192.168.1.1/centos-5.7-i386
lang en_US.UTF-8
keyboard us
clearpart --all
autopart
network --device eth0 --bootproto dhcp --hostname newborn
rootpw cfengine
firewall --enabled --port=22:tcp --port=22:tcp
authconfig --enableshadow --enablemd5
selinux --disabled
timezone --utc America/Los_Angeles
bootloader --location=mbr --driveorder=hda --append=\"rhgb quiet\"
reboot

%packages
@core
@base
device-mapper-multipath
-sysreport


%post
echo Downloading CFEngine RPM
wget http://192.168.1.1/cfengine-community-3.2.1-1.el5.i386.rpm
echo
echo

echo Downloading CFEngine inputs tar-ball
wget http://192.168.1.1/cfengine_inputs.tar
echo
echo

echo Installing CFEngine RPM
rpm -ihv cfengine-community-3.2.1-1.el5.i386.rpm
echo
echo

echo Removing the masterfiles that were shipped with 3.2.1
echo We provide our own policy set.
rm -f /var/cfengine/masterfiles/*
echo
echo

echo Extracting CFEngine policies
mkdir /var/cfengine/inputs >/dev/null 2>/dev/null
tar -C /var/cfengine/inputs -xvf cfengine_inputs.tar
echo
echo


echo Running CFEngine for the first time
/usr/local/sbin/cf-agent -I
"
        insert_type => "preserve_block";
}

bundle agent run_pxe_commands_to_setup_pxeboot {

  vars:
      "exec_result" string => execresult("/usr/sbin/pxeos -l", "noshell");

  classes:
      "centos_is_installed"
        expression => regcmp("centos-5.7-i386","$(exec_result)");

  commands:
    !centos_is_installed::
      "/usr/sbin/pxeos -a -i centos-5.7-i386 -p HTTP -D 0 -s 192.168.1.1 \
                       -L /centos-5.7-i386 \
                       -K 'http://192.168.1.1/centos-5.7-i386.ks' \
                        centos-5.7-i386";
      "/usr/sbin/pxeboot -a -O centos-5.7-i386 \
                         -K 'http://192.168.1.1/centos-5.7-i386.ks' \
                         -r 1000 192.168";

}
```
\end{codelisting}

<!---
Filename: 960-410-Monitor-0010-title\_card.md
-->

## CFEngine Monitor daemon


\begin{codelisting}
\codecaption{960-410-Monitor-0020-Example\_of\_using\_monitoring.cf}
```cfengine3, options: "linenos": true
# report environmental conditions

bundle agent main {

  vars:
      "threshold" int => "50";

      ##########################################3
  classes:

      "CPU_load_high"
        expression => isgreaterthan("$(mon.value_cpu)","$(threshold)");

  reports:
    CPU_load_high::
      "!!!!! CPU LOAD IS OVER THRESHOLD OF $(threshold) percent !!!! ";

}
```
\end{codelisting}
\begin{codelisting}
\codecaption{960-420-CFEngine\_Templates-0379-template.cf}
```cfengine3, options: "linenos": true
# CFEngine template using insert_type and expand_scalars
#
# Create a file /tmp/template.dat which contains:
#
# Dear $(write_letter.first_name),
#
#   Please buy our product.
#
# Love, Company

bundle agent main {

  vars:
     "public"
     slist => { "Mary", "Joe", "Ann", "Ed" };

  methods:
      "Promotional campaign"
        usebundle => write_letter("$(public)");
}
      

bundle agent write_letter(first_name) {

  files:

      "/tmp/letter_to_$(first_name).txt"

        handle    => "write_letter",
        create    => "true",
        edit_line => create_from_template;
}

bundle edit_line create_from_template {

  insert_lines:

      "/tmp/template.dat"

        handle => "insert_expanded_template",
        insert_type => "file",
        expand_scalars => "true";
}
```
\end{codelisting}
