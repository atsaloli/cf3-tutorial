# In CFEngine, you can document not only the promiser
# (what makes the promise) but also the promisee 
# (to whom the promise is made, or what depends on
# that promise).
#
# The following example demostrates that 'fuel_check'
# has a documented impact on 'ignition', and that
# 'ignition depends on 'fuel_check'
#
# Originally, depends_on was for knowledge management only,
# but as of version 3.6, it controls policy flow, so that
# when you run this example, you will see the 'Launch'
# report evaluated only after 'fuel_check' is successful:
#
# R: Fueling
# R: Launch!!


bundle agent main
{

  reports:

      "Launch!!"
        depends_on => { "fuel_check" },
        handle => "ignition",
        comment => "Demonstrate flow control with depends_on";

      "Fueling" -> { "ignition" }
        handle => "fuel_check",
        comment => "Demonstrate promisee";

}
# Try to think declaratively (not imperatively), and use depends_on
# only when needed.
