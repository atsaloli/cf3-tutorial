# This is an example "policy update" policy that promises that
# /var/cfengine/inputs will be a copy of the hub's
# /var/cfengine/masterfiles
#
# The following what the update policy looked like early on
# (near CFEngine 3.0).
#
# Now the update policy is more sophisticated, with
# some tricks to improve performance, although fundamentally, 
# what the update policy does is still: ensure that local directory
# /var/cfengine/inputs is a copy of the hub's /var/cfengine/masterfiles

bundle agent main {

  vars:

      "remote_path" string => "/var/cfengine/masterfiles";
      "remote_server" string => "$(sys.policy_hub)";

  files:

      "/var/cfengine/inputs"

        handle => "update_inputs_dir",
        comment => "Pull down latest policy set",
        perms => m("600"),
        copy_from => remote_cp("$(remote_path)","$(remote_server)"),
        depth_search => recurse("inf"),
        action => immediate;
}

body perms m(mode)
{
        mode  => "$(mode)";
}

body copy_from remote_cp(from,server)
{
        servers     => { "$(server)" };
        source      => "$(from)";
        compare     => "mtime";
        trustkey    => "true";  # trust the server's public key
}

body depth_search recurse(d)
{
        depth => "$(d)";
        xdev  => "true";
}

body action immediate
{
        ifelapsed => "0";
}
