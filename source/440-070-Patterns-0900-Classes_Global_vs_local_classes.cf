# Classes defined in common bundles are global.
#
# They appear in the Defined Classes section at the start of
# verbose output.
#
# Classes defined in all other bundles are local. You will see
# them defined in verbose mode as well (the "C" stands for classes):
#
# verbose: C: BEGIN classes / conditions (pass 1)
# verbose: C: .........................................................
# verbose: C:     +  Private class: local_class

body common control
{
  bundlesequence => {
                      "g",
                      "example_1",
                      "example_2",
                    };
}


bundle common g {
  classes:
    any::
      "global_class";
}


bundle agent example_1
{
  classes:
    any::
      "local_class";

  reports:
    global_class:: "Bundle $(this.bundle): global class 'global_class' detected";
    local_class::  "Bundle $(this.bundle): local class 'local_class' detected";
}


bundle agent example_2
{
  reports:
    global_class:: "Bundle $(this.bundle): global class 'global_class' detected";
    local_class::  "Bundle $(this.bundle): local class 'local_class' detected";
}

# Output:
#
# R: Bundle example_1: global class 'global_class' detected
# R: Bundle example_1: local class 'local_class' detected
# R: Bundle example_2: global class 'global_class' detected
